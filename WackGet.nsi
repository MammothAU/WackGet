; Generated NSIS script file (generated by makensitemplate.phtml 0.21)
; by 64.81.53.7 on Jan 06 02 @ 03:16

; NOTE: this .NSI script is designed for NSIS v1.8+

!define APP_VERSION "1.2.3" ; This comes from makefile now

Name "WackGet"
OutFile "WackGet${APP_VERSION}.exe"

; Some default compiler settings (uncomment and change at will):
SetCompress auto ; (can be off or force)
; SetDatablockOptimize on ; (can be off)
; CRCCheck on ; (can be off)
; AutoCloseWindow false ; (can be true for the window go away automatically at end)
; ShowInstDetails hide ; (can be show to have them shown, or nevershow to disable)
; SetDateSave off ; (can be on to have files restored to their orginal date)

LicenseText "WackGet is free software released under the terms of the GNU General Public License."
LicenseData "LICENSE.txt"

InstallDir "$PROGRAMFILES\WackGet"
InstallDirRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Millweed\WackGet" ""
;DirShow show ; (make this hide to not let the user change it)
DirText "Setup has determined the default location to install.  To change the location, click Browse below or enter a path manually." "Select the installation folder:"

ComponentText "This will install WackGet ${APP_VERSION}" "" "Install with the following components and options:"

Function check_directory
IfFileExists "$INSTDIR\WGDLL.dll" +1 no_message
MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION|MB_DEFBUTTON2 "It looks like WackGet is already installed there.  You should cancel and uninstall the previous version first." IDOK no_message
Abort
no_message:
FunctionEnd

Page license
Page components
Page directory "" "" check_directory
Page instfiles

Section "WackGet (required)" ;
SectionIn RO
SetOutPath "$INSTDIR"
File "WackGet.exe"
File "WGDLL.dll"
File "WGET.EXE"
File "cacert.pem"
File "README.txt"
File "LICENSE.txt"
File "OpenSSL_LICENSE.txt"
File "CHANGES.txt"
File "WackGet.html"
File "WGDLL\wgbho.js"
File "libeay32.dll"
File "ssleay32.dll"
CreateDirectory "$SMPROGRAMS\WackGet"
CreateShortCut "$SMPROGRAMS\WackGet\WackGet.lnk" "$INSTDIR\WackGet.exe"
CreateShortCut "$SMPROGRAMS\WackGet\Uninstall WackGet.lnk" "$INSTDIR\uninst.exe"
CreateShortCut "$SMPROGRAMS\WackGet\WackGet Help.lnk" "$INSTDIR\WackGet.html"
; add files / whatever that need to be installed here.
WriteRegStr HKEY_LOCAL_MACHINE "SOFTWARE\Millweed\WackGet" "" "$INSTDIR"
WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\WackGet" "DisplayName" "WackGet (remove only)"
WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\WackGet" "UninstallString" '"$INSTDIR\uninst.exe"'
; write out uninstaller
WriteUninstaller "$INSTDIR\uninst.exe"
SectionEnd ; end of default section

Section "Associate .wgq files with WackGet" ;
WriteRegStr HKEY_CLASSES_ROOT ".wgq" "" "WackGet Queue"
WriteRegStr HKEY_CLASSES_ROOT "WackGet Queue\DefaultIcon" "" "$INSTDIR\WackGet.exe,0"
WriteRegStr HKEY_CLASSES_ROOT "WackGet Queue\shell\open\command" "" '$INSTDIR\WackGet.exe "%1"'
SectionEnd ;

; begin uninstall settings/section
UninstallText "This will uninstall WackGet from your system"

Section Uninstall
; add delete commands to delete whatever files/registry keys/etc you installed here.

delete_dll_retry:
UnRegDLL "$INSTDIR\WGDLL.dll"
Delete "$INSTDIR\WGDLL.dll"
IfFileExists "$INSTDIR\WGDLL.dll" +1 delete_dll_success
MessageBox MB_ABORTRETRYIGNORE|MB_ICONSTOP "Couldn't delete WGDLL.dll.  Please close all Internet Explorer windows and hit Retry." IDRETRY delete_dll_retry IDIGNORE delete_dll_success
Quit
delete_dll_success: ; success/ignore

Delete "$SMPROGRAMS\WackGet\WackGet.lnk"
Delete "$SMPROGRAMS\WackGet\Uninstall WackGet.lnk"
Delete "$SMPROGRAMS\WackGet\WackGet Help.lnk"
RMDir "$SMPROGRAMS\WackGet"
Delete "$INSTDIR\WackGet.exe"
Delete "$INSTDIR\WGET.EXE"
Delete "$INSTDIR\LICENSE.txt"
Delete "$INSTDIR\CHANGES.txt"
Delete "$INSTDIR\WackGet.html"
Delete "$INSTDIR\wgbho.js"
Delete "$INSTDIR\uninst.exe"
DeleteRegKey HKEY_CURRENT_USER "SOFTWARE\Millweed\WackGet"
DeleteRegKey /ifempty HKEY_CURRENT_USER "SOFTWARE\Millweed"
DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Millweed\WackGet"
DeleteRegKey /ifempty HKEY_LOCAL_MACHINE "SOFTWARE\Millweed"
DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\WackGet"
DeleteRegKey HKEY_CLASSES_ROOT ".wgq"
DeleteRegKey HKEY_CLASSES_ROOT "WackGet Queue"
RMDir "$INSTDIR"
SectionEnd ; end of uninstall section

; eof
